; *************************************
; BEGIN OF JAPANSE KEYBOARD HANDLER
; *************************************

I0DA5:	DEFB	00AH
	DEFW	C0E67		; keys 0,1,2,3,4,5,6,7,8,9
	DEFB	016H
	DEFW	C0EA1		; keys -,^,yen,@,[,;,:,],komma,.,/,_
	DEFB	030H
	DEFW	C0E7E		; keys a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,x,y,z
	DEFB	033H
	DEFW	C0F10		; keys shift,ctrl,graph
	DEFB	034H
	DEFW	C0F36		; key caps
	DEFB	035H
	DEFW	C0F1F		; key kana
	DEFB	03AH
	DEFW	C0EBB		; keys f1,f2,f3,f4,f5
	DEFB	03CH
	DEFW	C0F10		; keys esc,tab
	DEFB	03DH
	DEFW	C0F46		; key stop
	DEFB	041H
	DEFW	C0F10		; keys bs,select,return,space
	DEFB	042H
	DEFW	C0F06		; home
	DEFB	0FFH
	DEFW	C0F10		; ins,del,left,up,down,right, numeric pad
;
I0DC9:	DEFB	0FFH
	DEFB	"!",022H,"#$%&'()"
;
I0DD3:	DEFW	C0F55		; SHIFT + CTRL
	DEFW	C0F55		; CTRL
	DEFW	C0E93		; SHIFT
	DEFW	C0E95

I0DDB:	DEFW	D0E07-00AH	; SHIFT + CTRL
	DEFW	D0DFB-00AH	; CTRL
	DEFW	D0DEF-00AH	; SHIFT
	DEFW	D0DE3-00AH

D0DE3:	DEFB	"-^\@[;:],./",0FFH
D0DEF:	DEFB	"=~|`{+*}<>?_"
D0DFB:	DEFB	"-",01EH,01CH,0,01BH,";:",01DH,",./",0FFH
D0E07:	DEFB	"=",01EH,01CH,0,01BH,"+*",01DH,"<>?",01FH

D0E13:	DEFB	0,0,0,0,0,0,0,0
	DEFB	0,0,01BH,009H,0,008H,018H,00DH
	DEFB	" ",00CH,012H,07FH,01DH,01EH,01FH,01CH
	DEFB	"*","+","/","0","1","2","3","4"
	DEFB	"5","6","7","8","9","-",",","."

;	  Subroutine K.HAND (not offical name)
;	     Inputs  C = scancode
;	     Outputs ________________________
;

K.HAND:	LD	A,C
	CP	0FFH
	RET	Z			; scancode 0FFH, quit
	LD	HL,I0DA5
	CALL	H.KEYC
	CP	30H
	JR	NC,J0E5C		; scancodes 030H-0FEH are handled directly
	LD	A,(NEWKEY+6)
	RRCA	
	RRCA	
	JR	NC,J0E5B		; CTRL pressed, handle directly
	RRCA	
	JP	NC,J107D		; GRAPH pressed, handle graphic keycodes
	LD	A,(KANAST)
	AND	A
	JP	NZ,J0F83		; KANA on, handle kana keycodes
J0E5B:	LD	A,C
J0E5C:	CP	(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
J0E61:	INC	HL
	PUSH	DE
	RET	C
	POP	DE
	JR	J0E5C
;
;	-----------------
;	Numeric keys handler
;	-----------------

C0E67:	ADD	A,"0"
	LD	B,A
	LD	A,(NEWKEY+6)
	RRCA				; SHIFT pressed ?
	LD	A,B
	JR	C,J0E7B			; no
	LD	B,00H
	LD	HL,I0DC9
	ADD	HL,BC
	LD	A,(HL)
	CP	0FFH
	RET	Z
J0E7B:	JP	C0F55
;
;	-----------------
; 	a-z keys handler
;	-----------------

C0E7E:	LD	A,(NEWKEY+6)
	AND	03H			; SHIFT + CTRL status
	ADD	A,A
	LD	E,A
	LD	D,00H
	LD	HL,I0DD3
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	A,C
	SUB	15H
	JP	(HL)
;
; a-z keys, shift pressed, ctrl not pressed
;
C0E93:	ADD	A,20H			; lowercase
;
; a-z keys, no shift or ctrl pressed
;
C0E95:	LD	B,A
	LD	A,(CAPST)
	CPL	
	AND	20H
	XOR	B
	ADD	A,40H			; letter ascii
	JR	J0E7B
;
;	-----------------
C0EA1:	LD	HL,I0DDB
	LD	A,(NEWKEY+6)
	AND	03H			; SHIFT + CTRL status
	ADD	A,A
	LD	E,A
	LD	D,00H
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	E,C
	ADD	HL,DE
	LD	A,(HL)
	CP	0FFH
	JP	NZ,C0F55
	RET	
;
;	-----------------
;	Functionkey handler
;	-----------------

C0EBB:	LD	A,(NEWKEY+6)
	RRCA	
	JR	C,J0EC5		; No SHIFT,
;
	LD	A,C
	ADD	A,05H	; 5 
	LD	C,A
J0EC5:	LD	E,C
	LD	D,00H
	LD	HL,FNKFLG-035H
	ADD	HL,DE
	LD	A,(HL)
	AND	A
	JR	NZ,J0EE3
;
J0ED0:	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,FNKSTR-035H*16
	ADD	HL,DE
	EX	DE,HL
J0EDA:	LD	A,(DE)
	AND	A
	RET	Z
	CALL	C0F55
I0EE0:	INC	DE
	JR	J0EDA
;
;	-----------------
J0EE3:	LD	HL,(CURLIN)
	INC	HL
	LD	A,H
	OR	L
	JR	Z,J0ED0
;
	LD	HL,TRPTBL+0*3-035H*3
	ADD	HL,DE
	ADD	HL,DE
	ADD	HL,DE
;
;	  Subroutine __________________________
;	     Inputs  ________________________
;	     Outputs ________________________
;
C0EF1:	LD	A,(HL)
	AND	01H	; 1 
	RET	Z
;
	LD	A,(HL)
	OR	04H	; 4 
	CP	(HL)
	RET	Z
;
	LD	(HL),A
	XOR	05H	; 5 
	RET	NZ
;
	LD	A,(ONGSBF)
	INC	A
	LD	(ONGSBF),A
	RET	
;
;	-----------------
C0F06:	LD	A,(NEWKEY+6)
	RRCA	
	LD	A,0CH	; 12 
	SBC	A,00H
	JR	C0F55
;
;	-----------------

C0F10:	CALL	H.KEYA
	LD	E,A
	LD	D,00H
	LD	HL,D0E13-030H
	ADD	HL,DE
	LD	A,(HL)
	AND	A
	RET	Z
;
	JR	C0F55


;
; KANA keyhandler
;

;
; MSX 1 had:
;
;C0F1F:	LD	HL,KANAST
;	LD	A,(HL)
;	CPL	
;	LD	(HL),A
;	LD	A,0FH
;	OUT	(0A0H),A
;	IN	A,(0A2H)
;	AND	7FH
;	LD	B,A
;	LD	A,(HL)
;	CPL	
;	AND	80H
;	OR	B
;	OUT	(0A1H),A
;	RET
;
; MSX 2 and above have following code:
;
;
; original routine simply toggles KANA mode and led
; but when SHIFT KANA mode was added, there was too little space for code.
; led code was moved to 111C, most of the KANA key code was placed
; here, remainer was placed at 0C25-0C3B

C0F1F:	LD	HL,MODE
	BIT	0,(HL)				; in SHIFT KANA mode ?
	JR	Z,J0F2C				; nope,
;
	XOR	A				; KANA led off
	RES	0,(HL)				; leave SHIFT KANA mode
J0F29:	JP	J111C				; change KANA led
;
;	-----------------
J0F2C:	LD	A,(KANAST)
	INC	A				; KANA ON ?
	JP	Z,J0C36				; yes, KANA OFF
;
	JP	J0C25				; KANA ON or SHIFT KANA mode
;
; end of difference MSX1 - MSX2
;
;	-----------------
C0F36:	LD	HL,CAPST
	LD	A,(HL)
	CPL	
	LD	(HL),A
	CPL	

;	  Subroutine CHGCAP
;	     Inputs  ________________________
;	     Outputs ________________________
;
;	compatible with MSX1

K.BCAP:	AND	A
	LD	A,0CH
	JR	Z,J0F43
	INC	A
J0F43:	OUT	(0ABH),A
	RET	
;
;	compatible with MSX1

C0F46:	LD	A,(NEWKEY+6)
	RRCA	
	RRCA	
	LD	A,03H	; 3 
	JR	NC,J0F50
;
	INC	A
J0F50:	LD	(INTFLG),A
	JR	C,J0F64
;
;
;	  Subroutine __________________________
;	     Inputs  ________________________
;	     Outputs ________________________
;
;	compatible with MSX1

;
; MSX 1 had:
;
;	LD	HL,(PUTPNT)
;	LD	(HL),A
;	CALL	C10C2
;	LD	A,(GETPNT)
;	CP	L
;	RET	Z
;	LD	(PUTPNT),HL

;
; MSX 2 and above have following code:
;
; clearly this is patchcode
; this was needed for handeling the new SHIFT KANA mode

C0F55:	POP	HL
	PUSH	HL
	LD	BC,I0EE0
	OR	A
	SBC	HL,BC				; called from functionkeyhandler ?
	LD	IX,S.PUTCHR
	JP	C029B

;
; end of difference MSX1 - MSX2
;

J0F64:	LD	A,(CLIKSW)
	AND	A
	RET	Z
;
	LD	A,(CLIKFL)
	AND	A
	RET	NZ
;
	LD	A,0FH	; 15 
	LD	(CLIKFL),A
	OUT	(0ABH),A
	LD	A,0AH	; 10 
J0F77:	DEC	A
	JR	NZ,J0F77
;
K.BSND:	AND	A
	LD	A,0EH
	JR	Z,J0F80
	INC	A
J0F80:	OUT	(0ABH),A
	RET	
;
;	-----------------
J0F83:	LD	A,(KANAMD)
	AND	A			; ANSI layout ?
	LD	A,(NEWKEY+6)
	RRCA				; shift status in Cx
	JR	Z,J0F97			; yep,
;
; JIS layout
;
	LD	HL,I101D
	JR	C,J0F9F
;
	LD	HL,I104D
	JR	J0F9F
;
; ANSI layout
;
J0F97:	LD	HL,I0FBD
	JR	C,J0F9F
;
	LD	HL,I0FED
J0F9F:	LD	B,00H
	ADD	HL,BC
	LD	BC,C0F55
	PUSH	BC
	LD	A,(CAPST)
	AND	A
	LD	A,(HL)
	RET	NZ			; CAPS lock active, quit
;
	CP	0A6H
	RET	C
;
	CP	0B0H
	RET	Z
;
	CP	0DEH
	RET	NC
;
	SUB	20H	; " "
	CP	0A0H
	RET	C
;
	ADD	A,40H	; "@"
	RET	
;
; ANSI layout, without SHIFT (based on CAPS on)
;
I0FBD:	DEFB	0C9H,0B1H,0B2H,0B3H,0B4H,0B5H,0C5H,0C6H
	DEFB	0C7H,0C8H,0D7H,0D8H,0D9H,0DAH,0DBH,0D3H
	DEFB	0DEH,0DFH,0D6H,0DCH,0A6H,0DDH,0BBH,0C4H
	DEFB	0C2H,0BDH,0B8H,0BEH,0BFH,0CFH,0CCH,0D0H
	DEFB	0D1H,0D2H,0D5H,0D4H,0CDH,0CEH,0B6H,0B9H
	DEFB	0BCH,0BAH,0CBH,0C3H,0B7H,0C1H,0CAH,0C0H
;
; ANSI layout, with SHIFT (based on CAPS on)
;
I0FED:	DEFB	0C9H,0A7H,0A8H,0A9H,0AAH,0ABH,0C5H,0C6H
	DEFB	0C7H,0C8H,0D7H,0D8H,0D9H,0DAH,0A2H,0D3H
	DEFB	0B0H,0A3H,0AEH,0A4H,0A1H,0A5H,0BBH,0C4H
	DEFB	0AFH,0BDH,0B8H,0BEH,0BFH,0CFH,0CCH,0D0H
	DEFB	0D1H,0D2H,0ADH,0ACH,0CDH,0CEH,0B6H,0B9H
	DEFB	0BCH,0BAH,0CBH,0C3H,0B7H,0C1H,0CAH,0C0H
;
; JIS layout, without SHIFT (based on CAPS on)
;
I101D:	DEFB	0DCH,0C7H,0CCH,0B1H,0B3H,0B4H,0B5H,0D4H
	DEFB	0D5H,0D6H,0CEH,0CDH,0B0H,0DEH,0DFH,0DAH
	DEFB	0B9H,0D1H,0C8H,0D9H,0D2H,0DBH,0C1H,0BAH
	DEFB	0BFH,0BCH,0B2H,0CAH,0B7H,0B8H,0C6H,0CFH
	DEFB	0C9H,0D8H,0D3H,0D0H,0D7H,0BEH,0C0H,0BDH
	DEFB	0C4H,0B6H,0C5H,0CBH,0C3H,0BBH,0DDH,0C2H
;
; JIS layout, with SHIFT (based on CAPS on)
;
I104D:	DEFB	0A6H,0C7H,0CCH,0A7H,0A9H,0AAH,0ABH,0ACH
	DEFB	0ADH,0AEH,0CEH,0CDH,0B0H,0DEH,0A2H,0DAH
	DEFB	0B9H,0A3H,0A4H,0A1H,0A5H,0DBH,0C1H,0BAH
	DEFB	0BFH,0BCH,0A8H,0CAH,0B7H,0B8H,0C6H,0CFH
	DEFB	0C9H,0D8H,0D3H,0D0H,0D7H,0BEH,0C0H,0BDH
	DEFB	0C4H,0B6H,0C5H,0CBH,0C3H,0BBH,0DDH,0AFH

;	  Subroutine handles scancodes 000H-02FH with GRAPH pressed
;	     Inputs  ________________________
;	     Outputs ________________________
;

J107D:	LD	B,00H
	LD	HL,I1092
	ADD	HL,BC
	LD	A,(HL)
	AND	A
	RET	Z
	CP	80H
	PUSH	AF
	LD	A,1
	CALL	C,C0F55
	POP	AF
	JP	C0F55

;	  Table with keycodes for scancodes 000H-02FH with GRAPH pressed
;	000H		no keycode
;	001H-07FH	keycode 1 + keycode (special MSX graphic character)
;	080H-0FFH	keycode

I1092:	DEFB	"O" ,"G" ,"A" ,"B" ,"C" ,"D" ,"E" ,"F"
	DEFB	"M" ,"N" ,"W" ,000H,"I" ,000H,084H,082H
	DEFB	081H,085H,"_" ,"]" ,080H,083H,000H,"["
	DEFB	"Z" ,"T" ,"X" ,"U" ,"S" ,"J" ,"V" ,000H
	DEFB	000H,"^" ,"K" ,000H,000H,"P" ,000H,"R"
	DEFB	"L" ,"Y" ,000H,"Q" ,000H,05CH,"H" ,000H



; *************************************
; END OF JAPANSE KEYBOARD HANDLER
; *************************************
